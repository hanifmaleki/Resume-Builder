plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '2.2.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id "org.openapi.generator" version "4.2.3"
}

apply plugin: 'idea'

group 'mlk.hanif'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

sourceSets{
    main {
        java {
            srcDir "$rootDir/src/main/generated"
        }
    }
}

clean.doFirst {
    delete "$rootDir/src/main/generated/"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    compile group: 'org.apache.velocity', name: 'velocity-engine-core', version: '2.2'
//    compile group: 'org.apache.velocity', name: 'velocity-tools', version: '2.0'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '2.0-M1-groovy-2.5'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'
    compile group: 'io.swagger', name: 'swagger-annotations', version: '1.5.0'
    compile 'org.openapitools:jackson-databind-nullable:0.1.0'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}


openApiGenerate {
    generatorName = "spring"
    library = "spring-mvc"
    outputDir = "$rootDir"
    inputSpec = "$rootDir/src/main/resources/ws-specification/resume.yaml"
    apiPackage = "mlk.hanif.resumeBuilder.restApi.api"
    modelPackage = "mlk.hanif.resumeBuilder.restApi.model"
    modelNameSuffix = "Resource"
    generateModelTests = false
    generateModelDocumentation = false
    generateApiTests = false
    generateApiDocumentation = false
    configOptions = [
            interfaceOnly  : "true",
            implicitHeaders: "false",
            skipDefaultInterface : 'true',
            sourceFolder: 'src/main/generated'
    ]
    systemProperties = [
            apis  : "",
            models: "",
            supportingFiles : "ApiUtil.java"
    ]
}



compileJava.dependsOn tasks.openApiGenerate